---
title: Serial.print()
---




= print()


// OVERVIEW SECTION STARTS
[#overview]
--

[float]
=== Beschreibung
Druckt Daten an den seriellen Anschluss als von Menschen lesbarer ASCII-Text. Dieser Befehl kann viele Formen annehmen.
Zahlen werden für jede Ziffer mit einem ASCII-Zeichen gedruckt. Floats werden in ähnlicher Weise als ASCII-Ziffern gedruckt. Die Standardeinstellung ist zwei Dezimalstellen. Bytes werden als einzelnes Zeichen gesendet.
Zeichen und Zeichenfolgen werden unverändert gesendet. Zum Beispiel:

* `_Serial_.print(78)` gibt "78" aus +
* `_Serial_.print(1.23456)` gibt "1.23" aus +
* `_Serial_.print('N')` gibt "N" aus +
* `_Serial_.print("Hello world.")` gibt "Hello world."


Ein optionaler zweiter Parameter gibt die Basis (Format) an, die verwendet werden soll. Zulässige Werte sind `BIN(Binär, oder Basis 2)`, `OCT(Oktal, oder Basis 8)`, `DEC(Dezimal, oder Basis 10)`, `HEX(Hexadezimal, oder Basis 16)`.
Bei Gleitkommazahlen gibt dieser Parameter die Anzahl der zu verwendenden Dezimalstellen an. Zum Beispiel:

* `_Serial_.print(78, BIN)` gibt "1001110" aus +
* `_Serial_.print(78, OCT)` gibt "116" aus +
* `_Serial_.print(78, DEC)` gibt "78" aus +
* `_Serial_.print(78, HEX)` gibt "4E" aus +
* `_Serial_.print(1.23456, 0)` gibt "1" aus +
* `_Serial_.print(1.23456, 2)` gibt "1.23" aus +
* `_Serial_.print(1.23456, 4)` gibt "1.2346"

Du kannst Flash-Speicher-basierte Zeichenfolgen an `_Serial_.print()` übergeben, indem du Sie mit link:../../../../variables/utilities/progmem[F()] einschließt. Zum Beispiel:

`_Serial_.print(F(“Hello World”))`

Um Daten ohne Umwandlung in ihre Darstellung als Zeichen zu senden, verwende link:../write[Serial.write()].
[%hardbreaks]


[float]
=== Syntax
`_Serial_.print(val)` +
`_Serial_.print(val, format)`


[float]
=== Parameter
`_Serial_`: Serielles Port-Objekt. Die Liste der verfügbaren seriellen Anschlüsse für jedes Board auf der link:../../serial[Serial-Hauptseite]. +
`val`: Der zu druckende Wert. Erlaubte Datentypen: Beliebiger Datentyp.


[float]
=== Rückgabewert
Gibt die Anzahl der geschriebenen Bytes zurück, das Lesen dieser Anzahl ist jedoch optional. Datentyp: `size_t`.

--
// OVERVIEW SECTION ENDS




// HOW TO USE SECTION STARTS
[#howtouse]
--

[float]
=== Beispielcode
// Beschreibe, worum es im Beispielcode geht und füge relevanten Code hinzu   ►►►►► DIESER ABSCHNITT IST OBLIGATORISCH ◄◄◄◄◄


[source,arduino]
----
/*
Verwendet eine for-Schleife, um Zahlen in verschiedenen Formaten zu drucken.
*/
void setup() {
  Serial.begin(9600); // Öffnet die serielle Schnittstelle bei 9600 Bit/s:
}

void loop() {
  // print labels
  Serial.print("NO FORMAT");  // Gibt einen Text aus
  Serial.print("\t");         // Gibt einen Tab aus

  Serial.print("DEC");
  Serial.print("\t");

  Serial.print("HEX");
  Serial.print("\t");

  Serial.print("OCT");
  Serial.print("\t");

  Serial.print("BIN");
  Serial.println();        // Carriage return nach dem letzten Text

  for (int x = 0; x < 64; x++) { // nur ein Teil des ASCII-Diagramms, ändere es entsprechend
    // Ausgabe in verschiedenen Formaten:
    Serial.print(x);       // Gibt eine ASCII-codierte Dezimalzahl aus - wie "DEC"
    Serial.print("\t\t");  // Gibt zwei Tabs aus, um die Textlänge auszugeben

    Serial.print(x, DEC);  // Ausgabe als ASCII-codierte Dezimalzahl
    Serial.print("\t");    // Gibt einen Tab aus

    Serial.print(x, HEX);  // Ausgabe als ASCII-codierte Hexadezimalzahl
    Serial.print("\t");    // Gibt einen Tab aus

    Serial.print(x, OCT);  // Ausgabe als ASCII-codierte Oktalzahl
    Serial.print("\t");    // Gibt einen Tab aus

    Serial.println(x, BIN);  // Ausgabe als ASCII-codierte Binärzahl
    // fügt dann den Carriage return mit "println" hinzu
    delay(200);            // Warte 200 Millisekunden
  }
  Serial.println();        // Gibt einen Carriage return aus
}
----
[%hardbreaks]

[float]
=== Anmerkungen und Warnungen
Informationen zur Asynchronität von `_Serial_.println()` findest du im Abschnitt Hinweise und Warnungen von link:../write#howtouse[Serial.write()].

--
// HOW TO USE SECTION ENDS


// SEE ALSO SECTION
[#see_also]
--

[float]
=== Siehe auch

[role="language"]
* #SPRACHE# link:../begin[begin()]
* #SPRACHE# link:../end[end()]
* #SPRACHE# link:../available[available()]
* #SPRACHE# link:../read[read()]
* #SPRACHE# link:../peek[peek()]
* #SPRACHE# link:../flush[flush()]
* #SPRACHE# link:../println[println()]
* #SPRACHE# link:../write[write()]
* #SPRACHE# link:../serialevent[SerialEvent()]
* #SPRACHE# link:https://www.arduino.cc/en/Tutorial/Memory[Memory]

--
// SEE ALSO SECTION ENDS
